SET QUOTED_IDENTIFIER ON
GO
SET ANSI_NULLS ON
GO

CREATE PROCEDURE [dbo].[spInfoTablaClassCreator]
(
	@nomtabla		nvarchar(100)
)

AS

SELECT
	OBJ.NAME AS NOM_TABLA,
	ISNULL((SELECT	EP.VALUE
			  FROM	SYS.EXTENDED_PROPERTIES EP
					INNER JOIN SYS.TABLES TB ON EP.MAJOR_ID = TB.OBJECT_ID
			WHERE	TB.NAME = @nomtabla  AND MINOR_ID = 0 AND EP.NAME = 'MS_Description'),'') AS DESC_TABLA,					
	COL.COLUMN_ID AS ORDEN_COLUMNA,
	COL.NAME AS NOM_COLUMNA,
	ISNULL((SELECT	COUNT(KCOL.COLUMN_NAME)
			FROM	INFORMATION_SCHEMA.KEY_COLUMN_USAGE KCOL
					INNER JOIN INFORMATION_SCHEMA.TABLE_CONSTRAINTS TBL	ON KCOL.CONSTRAINT_NAME = TBL.CONSTRAINT_NAME
			WHERE	KCOL.TABLE_NAME = @nomtabla
					AND TBL.CONSTRAINT_TYPE = 'Primary key'
					AND KCOL.COLUMN_NAME = COL.NAME), 0) AS PRIMARY_KEY,
	COL.IS_NULLABLE AS NULO, 
	COL.MAX_LENGTH AS LONGITUD,
	COL.IS_COMPUTED AS COMPUTED, 
	TYP.SYSTEM_TYPE_ID AS ID_TIPO,
	TYP.NAME AS NOM_TIPO,
	ISNULL((SELECT	EP.VALUE 
			FROM	SYS.EXTENDED_PROPERTIES EP
					INNER JOIN SYS.TABLES TB ON EP.MAJOR_ID = TB.OBJECT_ID
					INNER JOIN SYS.COLUMNS CL ON EP.MAJOR_ID = CL.OBJECT_ID  AND EP.MINOR_ID = CL.COLUMN_ID 	
			 WHERE 	TB.NAME = @nomtabla AND CL.NAME = COL.NAME  AND  EP.NAME = 'MS_Description'),'') AS DESC_COLUMNA ,
                 COL.IS_IDENTITY 
FROM	SYS.OBJECTS OBJ
	INNER JOIN SYS.COLUMNS COL ON OBJ.OBJECT_ID = COL.OBJECT_ID
	INNER JOIN SYS.TYPES TYP ON COL.SYSTEM_TYPE_ID = TYP.SYSTEM_TYPE_ID 
                     AND COL.USER_TYPE_ID = TYP.USER_TYPE_ID
WHERE	OBJ.NAME=@nomtabla
	AND OBJ.TYPE = 'U' 
ORDER BY ORDEN_COLUMNA ASC

GO
